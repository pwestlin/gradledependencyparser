import static org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED
import static org.gradle.api.tasks.testing.logging.TestLogEvent.SKIPPED

apply plugin: 'java'
apply plugin: 'idea'

archivesBaseName = 'GradleDependencyParser'
group = 'nu.westlin'
description = 'Gradle Dependency Parser'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "com.google.guava:guava:20.0"
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.1'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

    testCompile group: 'junit', name: 'junit', version: '4.12'

}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes 'Main-Class': 'nu.westlin.gradledependencyparser.Parser'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


tasks.withType(Test) {
    systemProperties = System.properties

    testLogging {
        events FAILED, SKIPPED

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'nu.westlin.gradledependencyparser.Parser'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}